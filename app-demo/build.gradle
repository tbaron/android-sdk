apply plugin: 'com.android.application'

android {
	compileSdkVersion 20
	buildToolsVersion "20.0.0"

	defaultConfig {
		applicationId "com.infospace.sdk.cards.demo"
		minSdkVersion 14
		targetSdkVersion 20
		versionCode 1
		versionName "1.0"
	}

	buildTypes {
		release {
			minifyEnabled true
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
	}

	packagingOptions {
		exclude 'META-INF/LICENSE.txt'
		exclude 'META-INF/NOTICE.txt'
	}
}

dependencies {
	compile project(':cards')
	compile 'commons-io:commons-io:2.4'
	compile('com.nhaarman.listviewanimations:lib-core:3.1.0@aar') {
		transitive = true
	}
}

def configPath = project.hasProperty('signingConfig') ? signingConfig : '.signing/release.signing.gradle'
def configFile = file(configPath)

if (configFile.exists()) {
	println 'Applying release signing configuration: ' + configFile.path
	apply from: configFile.path
} else {
	println 'Skipping release signing due to missing configuration. To enable signing, place configuration at: ' + configFile.path
}

if (project.hasProperty('versionCode')) {
	android.defaultConfig.versionCode = versionCode.toInteger()
}
if (project.hasProperty('versionName')) {
	android.defaultConfig.versionName = versionName
}
